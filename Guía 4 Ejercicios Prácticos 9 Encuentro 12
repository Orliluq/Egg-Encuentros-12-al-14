/// Los empleados de una fábrica trabajan en dos turnos: Diurno y Nocturno. Se desea calcular el
/// jornal diario de acuerdo con las siguientes reglas:
/// a) La tarifa de las horas diurnas es de $ 90
/// b) La tarifa de las horas nocturnas es de $ 125
/// c) En caso de ser feriado, la tarifa se incrementa en un 10% si el turno es diurno y en
/// un 15% si el turno es nocturno.
/// El programa debe solicitar la siguiente información al usuario: el nombre del trabajador, el día
/// de la semana, el turno (diurno o nocturno) y la cantidad de horas trabajadas. Además,
/// debemos preguntarle al usuario si el día de la semana (lunes, martes, miércoles, etc.) era
/// festivo o no, para poder calcular el jornal diario. Utilice una función para realizar el cálculo.


Funcion retornoJornal=FuncionJor(turno, horas, festivo)
	Definir retornoJornal Como Real
	Definir valorHoras Como Real
	
	retornoJornal = 0;
	
	Si turno = "diurno" Entonces
		valorHoras = 90;
		Si festivo = "S" Entonces
			valorHoras = 90 + (90*0.10);
		Fin Si
	SiNo
		valorHoras = 125;
		Si festivo = "S" Entonces
			valorHoras = 125 + (125*0.15);
		Fin Si
	Fin Si
	
	retornoJornal = horas * valorHoras;
	
Fin Funcion

Algoritmo Jornal
	Definir nom, day, turno, festivo Como Caracter
	Definir horas Como Entero
	Definir jor Como Real
	
	Escribir "Ingrese el nombre del trabajador: ";
	Leer nom;
	Escribir "Ingrese el día de la semana: ";
	Leer day;
	Escribir "Es festivo (S/N): ";
	Leer festivo;
	Escribir "Ingrese el turno (diurno/nocturno): ";
	Leer turno;
	Escribir "Ingrese las horas: ";
	Leer horas;
	
	jor = FuncionJor(turno, horas, festivo);
	
	Escribir "El jornal es: $", jor;
	
FinAlgoritmo
